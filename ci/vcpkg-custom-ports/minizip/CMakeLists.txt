cmake_minimum_required(VERSION 3.8)
project(minizip VERSION 1.2.13 LANGUAGES C)

find_package(ZLIB REQUIRED)
set(MIN_SRC contrib/minizip)

include_directories(${MIN_SRC} ${ZLIB_INCLUDE_DIRS})

set(MINIZIP_LIBRARIES ZLIB::ZLIB)

set(SRC
    ${MIN_SRC}/ioapi.c
    ${MIN_SRC}/unzip.c
    ${MIN_SRC}/zip.c
    ${MIN_SRC}/mztools.c
)

set(HEADERS
    ${MIN_SRC}/crypt.h
    ${MIN_SRC}/ioapi.h
    ${MIN_SRC}/unzip.h
    ${MIN_SRC}/zip.h
    ${MIN_SRC}/mztools.h
)

add_library(minizip ${SRC})

target_link_libraries(minizip PRIVATE ZLIB::ZLIB)
target_compile_definitions(minizip PRIVATE -D_ZLIB_H)

if(NOT DISABLE_INSTALL_TOOLS)
    add_executable(minizip_bin ${MIN_SRC}/minizip.c)
    add_executable(miniunz_bin ${MIN_SRC}/miniunz.c)

    target_link_libraries(minizip_bin minizip ${MINIZIP_LIBRARIES})
    target_link_libraries(miniunz_bin minizip ${MINIZIP_LIBRARIES})

    set_target_properties(minizip_bin PROPERTIES OUTPUT_NAME minizip)
    set_target_properties(miniunz_bin PROPERTIES OUTPUT_NAME miniunz)
endif()

install(
    TARGETS minizip
    EXPORT minizipTargets
    RUNTIME DESTINATION bin
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file("${PROJECT_BINARY_DIR}/minizipConfigVersion.cmake"
    COMPATIBILITY SameMajorVersion)

configure_package_config_file(${PROJECT_SOURCE_DIR}/cmake/minizipConfig.cmake.in
    minizipConfig.cmake
    INSTALL_DESTINATION share/minizip)

install(FILES
    "${PROJECT_BINARY_DIR}/minizipConfig.cmake"
    "${PROJECT_BINARY_DIR}/minizipConfigVersion.cmake"
    DESTINATION share/minizip
)

install(EXPORT minizipTargets
    NAMESPACE minizip::
    DESTINATION share/minizip
)

if(NOT DISABLE_INSTALL_TOOLS)
    install(
        TARGETS minizip_bin miniunz_bin
        RUNTIME DESTINATION tools/minizip
    )
endif()

if(NOT DISABLE_INSTALL_HEADERS)
    install(FILES ${HEADERS} DESTINATION include/minizip)
endif()